{load_file("nrngui.hoc")}
{load_file("defvar.hoc")}
load_file("common.hoc")

ncell = 10
objref cell, cells, nil, vec, vecs, r, netcons, nc
pnm = new ParallelNetManager(0)
pc = pnm.pc

cells = new List()
netcons = new List()

begintemplate Cell
public soma, vvec, ivec, syn
create soma
objref vvec, ivec, syn
proc init() {
  soma {
    insert pas  e_pas=-65  g_pas = 0.0001
    syn = new ExpSyn(.5)  syn.e = 0  syn.g = 0.001
    vvec = new Vector()
    vvec.record(&v(.5))
    ivec = new Vector()
    ivec.record(&i_pas(.5))
  }
}
endtemplate Cell

for (gid = pc.id; gid < ncell; gid += pc.nhost) {
  pc.set_gid2node(gid, pc.id)
if (1 && gid < pc.nhost) {
  cell = new Cell()
  cells.append(cell)
  pc.cell(gid, new NetCon(&cell.soma.v(.5), nil))
  continue
}
  cell = new NetStim()
  cell.start=1
  pc.cell(gid, new NetCon(cell, nil))
  cells.append(cell)
  nc = new NetCon(cell, cells.o(0).syn)
  netcons.append(nc)
  nc.delay = 1.0
  nc.weight[0] = 1.0
}

objref tvec, idvec
tvec = new Vector()
idvec = new Vector()
pc.spike_record(-1, tvec, idvec)

cvode.cache_efficient(1)
pc.set_maxstep(10)
stdinit()

// Run NEURON simulation
prun("")

objref vvecsav, ivecsav
vvecsav = cells.o(0).vvec.c()
ivecsav = cells.o(0).ivec.c()
cells.o(0).vvec.resize(0)
cells.o(0).ivec.resize(0)

// Run CoreNEURON simulation
use_coreneuron = 1
stdinit()
prun("")

if(vvecsav.eq(cells.o(0).vvec) && ivecsav.eq(cells.o(0).ivec)) {
  if(pc.id == 0) {
    printf("Test netstimdirect passed. Voltage and current vectors are same.\n")
  }
} else {
  execerror("Voltage or current vectors are different")
}

spike2file("netstimdirect")

pc.barrier()
quit()

