load_file("netparmpi.hoc")

objref pnm, pc, tvec, idvec

proc pcell() {localobj s
  s = new String()
  sprint(s.s, "t%g", t)
  pc.prcellstate($1, s.s)
}

proc spike2file() { localobj outf, s
    s = new String()
    sprint(s.s, "out%s.dat", $s1)
    outf = new File()
    if (pc.id == 0) {outf.wopen(s.s) outf.close }
    for pnm.serialize() {
        outf.aopen(s.s)
        for i=0, idvec.size-1 {
            outf.printf("%.8g\t%d\n", tvec.x[i], idvec.x[i])
        }
        outf.close
    }
}

proc prun() { localobj po
    if(dumpmodel) {
        pc.nrnbbcore_write("coredat")
    }
    if (use_coreneuron || gpu) {
        if (!nrnpython("from neuron import coreneuron")) {
            printf("Python not available\n")
            return
        }
        po = new PythonObject()
        po.coreneuron.enable = 1
        if(filemode) {
            po.coreneuron.filemode = 1
        }
        if(gpu) {
            po.coreneuron.gpu = 1
        }
        if(pc.id == 0) {
            printf("nrncore_arg: |%s|\n", po.coreneuron.nrncore_arg(tstop))
        }
    } else {
        tsav = startsw()
    }
    pc.psolve(tstop)
    if (!use_coreneuron && pc.id == 0) {
        printf("psolve time %g\n", startsw() - tsav)
    }
}
